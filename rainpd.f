      SUBROUTINE WRITEIMAGE

C     CREATE A FITS PRIMARY ARRAY
      INTEGER STATUS,UNIT,BLOCKSIZE,BITPIX,NAXIS,NAXES(2)
      INTEGER I,J,GROUP,FPIXEL,NELEMENTS,ARRAY(1,1)
      CHARACTER FILEMNAME*80
      LOGICAL SIMPLE,EXTEND

      STATUS=0
C     MNAME OF THE FITS FILE TO BE CREATED:
      FILEMNAME='pdclima.fits'
C     DELETE THE FILE IF EXISTS YET
      CALL DELETEFILE(FILEMNAME,STATUS)
C     GET AN UNUSED LOGICAL UNIT NUMBER TO USE TO CREATE THE FITS FILE
      CALL FTGIOU(UNIT,STATUS)
      PRINT *,'WRITEIMAGE:UNIT =',UNIT
      PRINT *,'WRITEIMAGE: CLIMATIC DATA 1961-1990'
      PRINT *,'WRITEIMAGE: TAKEN AT THE STATION'
      PRINT *,'WRITEIMAGE: PADUA (PADOVA) AIRPORT'
C     CREATE THE NEW EMPTY FITS FILE
      BLOCKSIZE=1
      CALL FTINIT(UNIT,FILEMNAME,BLOCKSIZE,STATUS)

C     INITIALIZE PARAMETERS ABOUT THE FITS IMAGE (300 X 200 16-BIT INTEGERS)
      SIMPLE=.TRUE.
      BITPIX=16
      NAXIS=0
      NAXES(1)=1
      NAXES(2)=1
      EXTEND=.TRUE.

C     WRITE THE REQUIRED HEADER KEYWORDS
      CALL FTPHPR(UNIT,SIMPLE,BITPIX,NAXIS,NAXES,0,1,EXTEND,STATUS)


C     CLOSE THE FILE AND FREE THE UNIT NUMBER
      CALL FTCLOS(UNIT, STATUS)
      CALL FTFIOU(UNIT, STATUS)
      END
C *************************************************************************
      SUBROUTINE PRINTERROR(STATUS)

C  THIS SUBROUTINE PRINTS OUT THE DESCRIPTIVE TEXT CORRESPONDING TO THE
C  ERROR STATUS VALUE AND PRINTS OUT THE CONTENTS OF THE INTERNAL
C  ERROR MESSAGE STACK GENERATED BY FITSIO WHENEVER AN ERROR OCCURS.

      INTEGER STATUS
      CHARACTER ERRTEXT*30,ERRMESSAGE*80

C  CHECK IF STATUS IS OK (NO ERROR); IF SO, SIMPLY RETURN
      IF (STATUS .LE. 0)RETURN

C  THE FTGERR SUBROUTINE RETURNS A DESCRIPTIVE 30-CHARACTER TEXT STRING THAT
C  CORRESPONDS TO THE INTEGER ERROR STATUS NUMBER.  A COMPLETE LIST OF ALL
C  THE ERROR NUMBERS CAN BE FOUND IN THE BACK OF THE FITSIO USER'S GUIDE.
      CALL FTGERR(STATUS,ERRTEXT)
      PRINT *,'FITSIO ERROR STATUS =',STATUS,': ',ERRTEXT

C  FITSIO USUALLY GENERATES AN INTERNAL STACK OF ERROR MESSAGES WHENEVER
C  AN ERROR OCCURS.  THESE MESSAGES PROVIDE MUCH MORE INFORMATION ON THE
C  CAUSE OF THE PROBLEM THAN CAN BE PROVIDED BY THE SINGLE INTEGER ERROR
C  STATUS VALUE.  THE FTGMSG SUBROUTINE RETRIEVES THE OLDEST MESSAGE FROM
C  THE STACK AND SHIFTS ANY REMAINING MESSAGES ON THE STACK DOWN ONE
C  POSITION.  FTGMSG IS CALLED REPEATEDLY UNTIL A BLANK MESSAGE IS
C  RETURNED, WHICH INDICATES THAT THE STACK IS EMPTY.  EACH ERROR MESSAGE
C  MAY BE UP TO 80 CHARACTERS IN LENGTH.  ANOTHER SUBROUTINE, CALLED
C  FTCMSG, IS AVAILABLE TO SIMPLY CLEAR THE WHOLE ERROR MESSAGE STACK IN
C  CASES WHERE ONE IS NOT INTERESTED IN THE CONTENTS.
      CALL FTGMSG(ERRMESSAGE)
      DO WHILE (ERRMESSAGE .NE. ' ')
          PRINT *,ERRMESSAGE
          CALL FTGMSG(ERRMESSAGE)
      END DO
      END
C *************************************************************************
      SUBROUTINE DELETEFILE(FILEMNAME,STATUS)

C  A SIMPLE LITTLE ROUTINE TO DELETE A FITS FILE

      INTEGER STATUS,UNIT,BLOCKSIZE
      CHARACTER*(*) FILEMNAME

C  SIMPLY RETURN IF STATUS IS GREATER THAN ZERO
      IF (STATUS .GT. 0)RETURN

C  GET AN UNUSED LOGICAL UNIT NUMBER TO USE TO OPEN THE FITS FILE
      CALL FTGIOU(UNIT,STATUS)

C  TRY TO OPEN THE FILE, TO SEE IF IT EXISTS
      CALL FTOPEN(UNIT,FILEMNAME,1,BLOCKSIZE,STATUS)

      IF (STATUS .EQ. 0)THEN
C         FILE WAS OPENED;  SO NOW DELETE IT
          CALL FTDELT(UNIT,STATUS)
      ELSE IF (STATUS .EQ. 103)THEN
C         FILE DOESN'T EXIST, SO JUST RESET STATUS TO ZERO AND CLEAR ERRORS
          STATUS=0
          CALL FTCMSG
      ELSE
C         THERE WAS SOME OTHER ERROR OPENING THE FILE; DELETE THE FILE ANYWAY
          STATUS=0
          CALL FTCMSG
          CALL FTDELT(UNIT,STATUS)
      END IF

C  FREE THE UNIT NUMBER FOR LATER REUSE
      CALL FTFIOU(UNIT, STATUS)
      END

      SUBROUTINE WRITEASCII

C  CREATE AN ASCII TABLE CONTAINING 3 COLUMNS AND 6 ROWS.  FOR CONVENIENCE,
C  THE ASCII TABLE EXTENSION IS APPENDED TO THE FITS IMAGE FILE CREATED
C  PREVIOUSLY BY THE WRITEIMAGE SUBROUTINE.

      INTEGER STATUS,UNIT,READWRITE,BLOCKSIZE,TFIELDS,NROWS,ROWLEN
      INTEGER NSPACE,TBCOL(4)
     &,COLNUM,FROW,FELEM
      REAL TMIN(12),TMAX(12),RAIN(12)
      CHARACTER FILEMNAME*40,EXTMNAME*16
      CHARACTER*16 TTYPE(4),TFORM(4),TUNIT(4),MNAME(12)
      DATA TTYPE/'MONTH','TMAX','TMIN','RAIN'/
      DATA TFORM/'A9','F4.1','F4.1','F5.1'/
      DATA TUNIT/' ','C','C','mm/m^2'/
      DATA MNAME/'JANUARY','FEBRUARY','MARCH','APRIL','MAY','JUNE',
     & 'JULY','AUGUST','SEPTEMBER','OCTOBER','NOVEMBER','DECEMBER'/
      DATA TMAX/5.7,8.8,13.1,17.5,22.4,26.0,28.4,27.9,24.5,18.8,
     & 11.5,6.5/
      DATA TMIN/-1.4,0.5,3.5,7.4,11.6,15.3,17.5,16.9,13.8,8.8,3.7,-0.4/
      DATA RAIN/70.4,56.9,67.0,68.1,78.6,88.0,64.2,79.8,58.2,65.5,
     & 86.7,62.4/
C  THE STATUS PARAMETER MUST ALWAYS BE INITIALIZED.
      STATUS=0

C  MNAME OF THE FITS FILE TO APPEND THE ASCII TABLE TO:
      FILEMNAME='pdclima.fits'

C  GET AN UNUSED LOGICAL UNIT NUMBER TO USE TO OPEN THE FITS FILE.
      CALL FTGIOU(UNIT,STATUS)

C  OPEN THE FITS FILE WITH WRITE ACCESS.
C  (READWRITE = 0 WOULD OPEN THE FILE WITH READONLY ACCESS).
      READWRITE=1
      CALL FTOPEN(UNIT,FILEMNAME,READWRITE,BLOCKSIZE,STATUS)

C  FTCRHD CREATES A NEW EMPTY FITS EXTENSION FOLLOWING THE CURRENT
C  EXTENSION AND MOVES TO IT.  IN THIS CASE, FITSIO WAS INITIALLY
C  POSITIONED ON THE PRIMARY ARRAY WHEN THE FITS FILE WAS FIRST OPENED, SO
C  FTCRHD APPENDS AN EMPTY EXTENSION AND MOVES TO IT.  ALL FUTURE FITSIO
C  CALLS THEN OPERATE ON THE NEW EXTENSION (WHICH WILL BE AN ASCII
C  TABLE).
      CALL FTCRHD(UNIT,STATUS)

C  DEFINE PARAMETERS FOR THE ASCII TABLE (SEE THE ABOVE DATA STATEMENTS)
      TFIELDS=4
      NROWS=12
      EXTMNAME='PDCLIMA_ASCII'

C  FTGABC IS A CONVENIENT SUBROUTINE FOR CALCULATING THE TOTAL WIDTH OF
C  THE TABLE AND THE STARTING POSITION OF EACH COLUMN IN AN ASCII TABLE.
C  ANY NUMBER OF BLANK SPACES (INCLUDING ZERO)  MAY BE INSERTED BETWEEN
C  EACH COLUMN OF THE TABLE, AS SPECIFIED BY THE NSPACE PARAMETER.
      NSPACE=1
      CALL FTGABC(TFIELDS,TFORM,NSPACE,ROWLEN,TBCOL,STATUS)

C  FTPHTB WRITES ALL THE REQUIRED HEADER KEYWORDS WHICH DEFINE THE
C  STRUCTURE OF THE ASCII TABLE. NROWS AND TFIELDS GIVE THE NUMBER OF
C  ROWS AND COLUMNS IN THE TABLE, AND THE TTYPE, TBCOL, TFORM, AND TUNIT
C  ARRAYS GIVE THE COLUMN MNAME, STARTING POSITION, FORMAT, AND UNITS,
C  RESPECTIVELY OF EACH COLUMN. THE VALUES OF THE ROWLEN AND TBCOL PARAMETERS
C  WERE PREVIOUSLY CALCULATED BY THE FTGABC ROUTINE.
      CALL FTPHTB(UNIT,ROWLEN,NROWS,TFIELDS,TTYPE,TBCOL,TFORM,TUNIT,
     &            EXTMNAME,STATUS)

C  WRITE MNAMES TO THE FIRST COLUMN, DIAMETERS TO 2ND COL., AND DENSITY TO 3RD
C  FTPCLS WRITES THE STRING VALUES TO THE MNAME COLUMN (COLUMN 1) OF THE
C  TABLE.  THE FTPCLJ AND FTPCLE ROUTINES WRITE THE DIAMETER (INTEGER) AND
C  DENSITY (REAL) VALUE TO THE 2ND AND 3RD COLUMNS.  THE FITSIO ROUTINES
C  ARE COLUMN ORIENTED, SO IT IS USUALLY EASIER TO READ OR WRITE DATA IN A
C  TABLE IN A COLUMN BY COLUMN ORDER RATHER THAN ROW BY ROW.
      FROW=1
      FELEM=1
      COLNUM=1
      CALL FTPCLS(UNIT,COLNUM,FROW,FELEM,NROWS,MNAME,STATUS)
      COLNUM=2
      CALL FTPCLE(UNIT,COLNUM,FROW,FELEM,NROWS,TMAX,STATUS)
      COLNUM=3
      CALL FTPCLE(UNIT,COLNUM,FROW,FELEM,NROWS,TMIN,STATUS)
      COLNUM=4
      CALL FTPCLE(UNIT,COLNUM,FROW,FELEM,NROWS,RAIN,STATUS)

C  THE FITS FILE MUST ALWAYS BE CLOSED BEFORE EXITING THE PROGRAM.
C  ANY UNIT NUMBERS ALLOCATED WITH FTGIOU MUST BE FREED WITH FTFIOU.
      CALL FTCLOS(UNIT, STATUS)
      CALL FTFIOU(UNIT, STATUS)

C  CHECK FOR ANY ERROR, AND IF SO PRINT OUT ERROR MESSAGES.
C  THE PRINTERROR SUBROUTINE IS LISTED NEAR THE END OF THIS FILE.
      IF (STATUS .GT. 0)CALL PRINTERROR(STATUS)
      END

      PROGRAM PDCLIM
        CALL WRITEIMAGE
        CALL WRITEASCII
      END
