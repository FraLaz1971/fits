      PROGRAM READCLM

C  THIS IS THE FITSIO COOKBOOK PROGRAM THAT CONTAINS AN ANNOTATED LISTING OF
C  VARIOUS COMPUTER PROGRAMS THAT READ AND WRITE FILES IN FITS FORMAT
C  USING THE FITSIO SUBROUTINE INTERFACE.  THESE EXAMPLES ARE
C  WORKING PROGRAMS WHICH USERS MAY ADAPT AND MODIFY FOR THEIR OWN
C  PURPOSES.  THIS COOKBOOK SERVES AS A COMPANION TO THE FITSIO USER'S
C  GUIDE THAT PROVIDES MORE COMPLETE DOCUMENTATION ON ALL THE
C  AVAILABLE FITSIO SUBROUTINES.

C  CALL EACH SUBROUTINE IN TURN:

      CALL READHEADER
      CALL READTABLE
      END
C *************************************************************************
      SUBROUTINE READHEADER

C  PRINT OUT ALL THE HEADER KEYWORDS IN ALL EXTENSIONS OF A FITS FILE

      INTEGER STATUS,UNIT,READWRITE,BLOCKSIZE,NKEYS,NSPACE,HDUTYPE,I,J
      CHARACTER FILENAME*80,RECORD*80

C  THE STATUS PARAMETER MUST ALWAYS BE INITIALIZED.
      STATUS=0

C  GET AN UNUSED LOGICAL UNIT NUMBER TO USE TO OPEN THE FITS FILE.
      CALL FTGIOU(UNIT,STATUS)

C     NAME OF FITS FILE 
      FILENAME='pdclima.fits'

C     OPEN THE FITS FILE, WITH READ-ONLY ACCESS.  THE RETURNED BLOCKSIZE
C     PARAMETER IS OBSOLETE AND SHOULD BE IGNORED. 
      READWRITE=0
      CALL FTOPEN(UNIT,FILENAME,READWRITE,BLOCKSIZE,STATUS)

      J = 0
100   CONTINUE
      J = J + 1

      PRINT *,'HEADER LISTING FOR HDU', J

C  THE FTGHSP SUBROUTINE RETURNS THE NUMBER OF EXISTING KEYWORDS IN THE
C  CURRENT HEADER DATA UNIT (CHDU), NOT COUNTING THE REQUIRED END KEYWORD,
      CALL FTGHSP(UNIT,NKEYS,NSPACE,STATUS)

C  READ EACH 80-CHARACTER KEYWORD RECORD, AND PRINT IT OUT.
      DO I = 1, NKEYS
          CALL FTGREC(UNIT,I,RECORD,STATUS)
          PRINT *,RECORD
      END DO

C  PRINT OUT AN END RECORD, AND A BLANK LINE TO MARK THE END OF THE HEADER.
      IF (STATUS .EQ. 0)THEN
          PRINT *,'END'
          PRINT *,' '
      END IF

C  TRY MOVING TO THE NEXT EXTENSION IN THE FITS FILE, IF IT EXISTS.
C  THE FTMRHD SUBROUTINE ATTEMPTS TO MOVE TO THE NEXT HDU, AS SPECIFIED BY
C  THE SECOND PARAMETER.   THIS SUBROUTINE MOVES BY A RELATIVE NUMBER OF
C  HDUS FROM THE CURRENT HDU.  THE RELATED FTMAHD ROUTINE MAY BE USED TO
C  MOVE TO AN ABSOLUTE HDU NUMBER IN THE FITS FILE.  IF THE END-OF-FILE IS
C  ENCOUNTERED WHEN TRYING TO MOVE TO THE SPECIFIED EXTENSION, THEN A
C  STATUS = 107 IS RETURNED.
      CALL FTMRHD(UNIT,1,HDUTYPE,STATUS)

      IF (STATUS .EQ. 0)THEN
C         SUCCESS, SO JUMP BACK AND PRINT OUT KEYWORDS IN THIS EXTENSION
          GO TO 100

      ELSE IF (STATUS .EQ. 107)THEN
C         HIT END OF FILE, SO QUIT
          STATUS=0
      END IF

C  THE FITS FILE MUST ALWAYS BE CLOSED BEFORE EXITING THE PROGRAM. 
C  ANY UNIT NUMBERS ALLOCATED WITH FTGIOU MUST BE FREED WITH FTFIOU.
      CALL FTCLOS(UNIT, STATUS)
      CALL FTFIOU(UNIT, STATUS)

C  CHECK FOR ANY ERROR, AND IF SO PRINT OUT ERROR MESSAGES.
C  THE PRINTERROR SUBROUTINE IS LISTED NEAR THE END OF THIS FILE.
      IF (STATUS .GT. 0)CALL PRINTERROR(STATUS)
      END
C *************************************************************************
      SUBROUTINE READTABLE

C  READ AND PRINT DATA VALUES FROM AN ASCII OR BINARY TABLE
C  THIS EXAMPLE READS AND PRINTS OUT ALL THE DATA IN THE ASCII AND
C  THE BINARY TABLES THAT WERE PREVIOUSLY CREATED BY WRITEASCII AND
C  WRITEBINTABLE.  NOTE THAT THE EXACT SAME FITSIO ROUTINES ARE
C  USED TO READ BOTH TYPES OF TABLES.

      INTEGER STATUS,UNIT,READWRITE,BLOCKSIZE,HDUTYPE,NTABLE
      INTEGER FELEM,NELEMS,NULLJ,NFOUND,IROW,COLNUM
      REAL NULLE,TMAX,TMIN,RAIN
      CHARACTER FILENAME*40,NULLSTR*1,MNAME*9,TTYPE(4)*10
      LOGICAL ANYNULL

C  THE STATUS PARAMETER MUST ALWAYS BE INITIALIZED.
      STATUS=0

C  GET AN UNUSED LOGICAL UNIT NUMBER TO USE TO OPEN THE FITS FILE.
      CALL FTGIOU(UNIT,STATUS)

C  OPEN THE FITS FILE PREVIOUSLY CREATED BY WRITEIMAGE
      FILENAME='pdclima.fits'
      READWRITE=0
      CALL FTOPEN(UNIT,FILENAME,READWRITE,BLOCKSIZE,STATUS)

C  LOOP TWICE, FIRST READING THE ASCII TABLE, THEN THE BINARY TABLE
      NTABLE=2

C  MOVE TO THE NEXT EXTENSION
          CALL FTMAHD(UNIT,NTABLE,HDUTYPE,STATUS)

          PRINT *,' '
          IF (HDUTYPE .EQ. 1)THEN
              PRINT *,'READING ASCII TABLE IN HDU ',NTABLE
          END IF

C  READ THE TTYPEN KEYWORDS, WHICH GIVE THE NAMES OF THE COLUMNS
          CALL FTGKNS(UNIT,'TTYPE',1,4,TTYPE,NFOUND,STATUS)
          WRITE(*,2000)TTYPE
2000      FORMAT(2X,"ROW   ",4A10)

C  READ THE DATA, ONE ROW AT A TIME, AND PRINT THEM OUT
          FELEM=1
          NELEMS=1
          NULLSTR=' '
          NULLJ=0
          NULLE=0.
          DO 10 IROW=1,12
C             FTGCVS READS THE MNAMES FROM THE FIRST COLUMN OF THE TABLE.
              COLNUM=1
              CALL FTGCVS(UNIT,COLNUM,IROW,FELEM,NELEMS,NULLSTR,MNAME,
     &                    ANYNULL,STATUS)

C             FTGCVE READS THE TMAX VALUES FROM THE SECOND COLUMN.
              COLNUM=2
              CALL FTGCVE(UNIT,COLNUM,IROW,FELEM,NELEMS,NULLE,TMAX,
     &                    ANYNULL,STATUS)

C             FTGCVE READS THE DENSITY VALUES FROM THE THIRD COLUMN.
              COLNUM=3
              CALL FTGCVE(UNIT,COLNUM,IROW,FELEM,NELEMS,NULLE,TMIN,
     &                    ANYNULL,STATUS)
              COLNUM=4
              CALL FTGCVE(UNIT,COLNUM,IROW,FELEM,NELEMS,NULLE,RAIN,
     &                    ANYNULL,STATUS)
              WRITE(*,2001)IROW,MNAME,TMAX,TMIN,RAIN
2001          FORMAT(I5,A10,3F9.1)

10         CONTINUE


C  THE FITS FILE MUST ALWAYS BE CLOSED BEFORE EXITING THE PROGRAM. 
C  ANY UNIT NUMBERS ALLOCATED WITH FTGIOU MUST BE FREED WITH FTFIOU.
      CALL FTCLOS(UNIT, STATUS)
      CALL FTFIOU(UNIT, STATUS)

C  CHECK FOR ANY ERROR, AND IF SO PRINT OUT ERROR MESSAGES.
C  THE PRINTERROR SUBROUTINE IS LISTED NEAR THE END OF THIS FILE.
      IF (STATUS .GT. 0)CALL PRINTERROR(STATUS)
      END
C *************************************************************************
      SUBROUTINE PRINTERROR(STATUS)

C  THIS SUBROUTINE PRINTS OUT THE DESCRIPTIVE TEXT CORRESPONDING TO THE
C  ERROR STATUS VALUE AND PRINTS OUT THE CONTENTS OF THE INTERNAL
C  ERROR MESSAGE STACK GENERATED BY FITSIO WHENEVER AN ERROR OCCURS.

      INTEGER STATUS
      CHARACTER ERRTEXT*30,ERRMESSAGE*80

C  CHECK IF STATUS IS OK (NO ERROR); IF SO, SIMPLY RETURN
      IF (STATUS .LE. 0)RETURN

C  THE FTGERR SUBROUTINE RETURNS A DESCRIPTIVE 30-CHARACTER TEXT STRING THAT
C  CORRESPONDS TO THE INTEGER ERROR STATUS NUMBER.  A COMPLETE LIST OF ALL
C  THE ERROR NUMBERS CAN BE FOUND IN THE BACK OF THE FITSIO USER'S GUIDE.
      CALL FTGERR(STATUS,ERRTEXT)
      PRINT *,'FITSIO ERROR STATUS =',STATUS,': ',ERRTEXT

C  FITSIO USUALLY GENERATES AN INTERNAL STACK OF ERROR MESSAGES WHENEVER
C  AN ERROR OCCURS.  THESE MESSAGES PROVIDE MUCH MORE INFORMATION ON THE
C  CAUSE OF THE PROBLEM THAN CAN BE PROVIDED BY THE SINGLE INTEGER ERROR
C  STATUS VALUE.  THE FTGMSG SUBROUTINE RETRIEVES THE OLDEST MESSAGE FROM
C  THE STACK AND SHIFTS ANY REMAINING MESSAGES ON THE STACK DOWN ONE
C  POSITION.  FTGMSG IS CALLED REPEATEDLY UNTIL A BLANK MESSAGE IS
C  RETURNED, WHICH INDICATES THAT THE STACK IS EMPTY.  EACH ERROR MESSAGE
C  MAY BE UP TO 80 CHARACTERS IN LENGTH.  ANOTHER SUBROUTINE, CALLED
C  FTCMSG, IS AVAILABLE TO SIMPLY CLEAR THE WHOLE ERROR MESSAGE STACK IN
C  CASES WHERE ONE IS NOT INTERESTED IN THE CONTENTS.
      CALL FTGMSG(ERRMESSAGE)
      DO WHILE (ERRMESSAGE .NE. ' ')
          PRINT *,ERRMESSAGE
          CALL FTGMSG(ERRMESSAGE)
      END DO
      END
C *************************************************************************
      SUBROUTINE DELETEFILE(FILENAME,STATUS)

C  A SIMPLE LITTLE ROUTINE TO DELETE A FITS FILE

      INTEGER STATUS,UNIT,BLOCKSIZE
      CHARACTER*(*) FILENAME

C  SIMPLY RETURN IF STATUS IS GREATER THAN ZERO
      IF (STATUS .GT. 0)RETURN

C  GET AN UNUSED LOGICAL UNIT NUMBER TO USE TO OPEN THE FITS FILE
      CALL FTGIOU(UNIT,STATUS)

C  TRY TO OPEN THE FILE, TO SEE IF IT EXISTS
      CALL FTOPEN(UNIT,FILENAME,1,BLOCKSIZE,STATUS)

      IF (STATUS .EQ. 0)THEN
C         FILE WAS OPENED;  SO NOW DELETE IT 
          CALL FTDELT(UNIT,STATUS)
      ELSE IF (STATUS .EQ. 103)THEN
C         FILE DOESN'T EXIST, SO JUST RESET STATUS TO ZERO AND CLEAR ERRORS
          STATUS=0
          CALL FTCMSG
      ELSE
C         THERE WAS SOME OTHER ERROR OPENING THE FILE; DELETE THE FILE ANYWAY
          STATUS=0
          CALL FTCMSG
          CALL FTDELT(UNIT,STATUS)
      END IF

C  FREE THE UNIT NUMBER FOR LATER REUSE
      CALL FTFIOU(UNIT, STATUS)
      END
